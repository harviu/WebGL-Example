
<!DOCTYPE html>

<html>

<head>
<title> WebGL &mdash; Simple Texture Mapping </title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<!-- <script type="text/javascript" src="glMatrix-0.9.5.min.js"></script> -->
<script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.8.1/gl-matrix-min.js"></script>

<!--  ************** Fragment Shader ************* --> 
<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;
    precision mediump int;

    uniform mat4 uMMatrix; 
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform mat4 uNMatrix; 

    uniform vec4 light_pos; 
    uniform vec4 ambient_coef;
    uniform vec4 diffuse_coef;
    uniform vec4 specular_coef;
    uniform float mat_shininess; 
    uniform float heightScale;
    uniform float dim_x;
    uniform float dim_y;

    uniform vec4 light_ambient; 
    uniform vec4 light_diffuse; 
    uniform vec4 light_specular;

    uniform int use_texture; 
    uniform sampler2D myTexture; // this is also used for normalmap

    varying vec4 eye_pos; 
    varying vec3 v_normal;
    varying vec3 v_tangent;
    varying highp vec2 FtexCoord; 
    varying vec4 vColor; 

    void main(void) {

        vec4 texcolor; 

        if ( use_texture == 1 ) {
            texcolor = texture2D(myTexture, FtexCoord); 
            gl_FragColor = texcolor; 
        }
        else if (use_texture == 3){
            // checker board pattern
            vec2 checker = floor(FtexCoord * vec2(10.0, 10.0));
            float checkerPattern = mod(checker.x + checker.y, 2.0);
            texcolor = mix(vec4(1.0, 1.0, 1.0, 1.0), vec4(0.0, 0.0, 0.0, 1.0), checkerPattern);
            gl_FragColor = texcolor;
        }
        else if (use_texture == 2) {
            vec3 normal = normalize( v_normal );
            vec3 tangent = normalize( v_tangent );
            vec3 binormal = cross(normal,tangent);
        
            // Texture coordinate offsets (based on texel size)
            vec2 texelSize = vec2(1.0 / dim_x, 1.0 / dim_y); 
        
            // Sample heights from neighboring texels
            float heightC = texture2D(myTexture, FtexCoord).r;
            float heightL = texture2D(myTexture, FtexCoord - vec2(texelSize.x, 0.0)).r;
            float heightR = texture2D(myTexture, FtexCoord + vec2(texelSize.x, 0.0)).r;
            float heightD = texture2D(myTexture, FtexCoord - vec2(0.0, texelSize.y)).r;
            float heightU = texture2D(myTexture, FtexCoord + vec2(0.0, texelSize.y)).r;
        
            // Compute slope/gradient (sobel-like)
            float du = (heightR - heightL) * heightScale;
            float dv = (heightU - heightD) * heightScale;
        
            // Construct bumped normal in tangent space
            vec3 bump_vector = du * tangent + dv * binormal;
            vec3 bumped_normal = normalize(vec3(uNMatrix * vec4(normal + bump_vector, 0.0)));
        
            // Lighting (same as before)
            vec4 light_pos_in_eye = light_pos;
            vec3 light_vector = normalize(vec3(light_pos_in_eye - eye_pos));
            vec3 eye_vector = normalize(-vec3(eye_pos));
        
            vec4 ambient = ambient_coef * light_ambient;
            float ndotl = max(dot(bumped_normal, light_vector), 0.0);
        
            vec4 diffuse = diffuse_coef * light_diffuse * ndotl;
        
            vec3 R = normalize(2.0 * ndotl * bumped_normal - light_vector);
            float rdotv = max(dot(R, eye_vector), 0.0);
        
            vec4 specular;
            if (ndotl > 0.0)
                specular = specular_coef * light_specular * pow(rdotv, mat_shininess);
            else
                specular = vec4(0.0);
        
            vec4 color = ambient + diffuse + specular;
            gl_FragColor = color;
        }
        else {
            gl_FragColor = vColor;
        }
    }
</script>

<!--  ************** Vertex Shader ************* --> 
<script id="shader-vs" type="x-shader/x-vertex">

    precision mediump float;
    precision mediump int; 

    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aVertexTexCoords; 
    attribute vec4 aVertexColor;
    attribute vec3 aVertexTangent;

    uniform mat4 uMMatrix; 
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform mat4 uNMatrix; 

    uniform vec4 light_pos; 
    uniform vec4 ambient_coef;
    uniform vec4 diffuse_coef;
    uniform vec4 specular_coef;
    uniform float mat_shininess; 

    uniform vec4 light_ambient; 
    uniform vec4 light_diffuse; 
    uniform vec4 light_specular;

    uniform int use_texture; 
    uniform sampler2D myTexture; 

    varying vec4 eye_pos;  //vertex position in eye space 
    varying vec3 v_normal;  // vertex normal
    varying vec3 v_tangent;
    varying highp vec2 FtexCoord; 
    varying vec4 vColor; 


    void main(void) {

        // transform light pos from local to eye space
        // vec4 light_pos_in_eye = uVMatrix * uMMatrix * light_pos;
	    vec4 light_pos_in_eye = light_pos;   

        // transform normal from local to eye space: normal matrix is the inverse transpose of the modelview matrix 
	    v_normal =normalize(vec3(uNMatrix*vec4(aVertexNormal,0.0)));
	    v_tangent =normalize(vec3(uNMatrix*vec4(aVertexTangent,0.0)));

        // transform the vertex position to eye space 
        eye_pos = uVMatrix*uMMatrix*vec4(aVertexPosition, 1.0);

        // light vector L = l-p 
        vec3 light_vector = normalize(vec3(light_pos_in_eye - eye_pos)); 

        // eye vector V = e-p, where e is (0,0,0) 
        vec3 eye_vector = normalize(-vec3(eye_pos));
        
        vec4 ambient = ambient_coef * light_ambient; 
        float ndotl = max(dot(v_normal, light_vector), 0.0); 

        vec4 diffuse = diffuse_coef * light_diffuse* ndotl;

//      both lines below are okay. One is to use the reflect function the other is to compute by yourself 
//      vec3 R= normalize(vec3(reflect(-light_vector, v_normal))); 
	    vec3 R = normalize(2.0 * ndotl *v_normal-light_vector);
        float rdotv = max(dot(R, eye_vector), 0.0);


        vec4 specular;  
        if (ndotl>0.0) 
        specular = specular_coef* light_specular*pow(rdotv, mat_shininess); 
        else
        specular = vec4(0,0,0,1);  

        vColor = ambient+diffuse+specular;

	    FtexCoord = aVertexTexCoords; 

        gl_Position = uPMatrix*uVMatrix*uMMatrix*vec4(aVertexPosition, 1.0);

    }
</script>


<script type="text/javascript" src="shaders_setup.js"></script>
<script type="text/javascript" src="texture.js"></script>


</head>


<body onload="webGLStart();">
    <canvas
    id="code10-canvas"style="border:none;"width="700"height="700"></canvas>

    <h4> Texture:  </h4>
    <button  onclick="texture(0)"> NO</button>
    <button  onclick="texture(3)"> Checker Board</button>
    <h5> Texture Mapping </h5>
    <button  onclick="texture(1, 'earth.png')"> Earth </button>
    <button  onclick="texture(1, 'brick.png')"> Brick </button>
    <h5> Bump Mapping </h5>
    <button  onclick="texture(2, 'height_map/brick-height-map.jpg')">Brick</button>
    <button  onclick="texture(2, 'height_map/dimples-height-map.png')">Dimples</button>
    <button  onclick="texture(2, 'height_map/random-height-map.png')">Noise</button>
    <button  onclick="texture(2, 'height_map/marble-height-map.png')">Marble</button>
    <button  onclick="texture(2, 'height_map/metal-height-map.png')">Metal</button>

    <h4> Bump Strength:  </h4>
    <label for="bumpStrength"><span id="bumpValue">1.00</span></label>
    <input type="range" id="bumpStrength" min="-5" max="5" step="0.05" value="1.0">

    <h4> Background:  </h4>
    <button  onclick="BG(1,0,0)"> Red</button>
    <button  onclick="BG(0,1,0)"> Green</button>
    <button  onclick="BG(0,0,1)"> Blue</button>
    <button  onclick="BG(0.5,0.5,0.5)"> Grey</button>
    <button  onclick="BG(0,0,0)"> Black</button>
    <button  onclick="BG(1,1,1)"> White</button>

    <div>
    <h4>
    Click on the left mouse button and move the coursor to rotate
    </h4>
    <button  onclick="redraw()"> Go Back! </button>
    </div>

</body>

</html>

