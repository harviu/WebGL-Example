
<!DOCTYPE html>

<html>

<head>
<title>Torii with Shadow</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<!--  ************** Fragment Shader ************* --> 
<script id="shader-fs" type="x-shader/x-fragment">
  precision mediump float;

  uniform vec4 ambient_coef;
  uniform vec4 diffuse_coef;
  uniform vec4 specular_coef;
  uniform vec4 light_color;
  uniform float mat_shininess; 
  uniform sampler2D uShadowMap;

  varying vec4 eye_pos;  //vertex position in eye space 
  varying vec3 v_normal;  // vertex normal
  varying vec3 light_v; //parallel light vector in eye
  varying vec3 eye_vector;
  varying vec4 vFragPosLightSpace;

  float shadowCalculation(vec4 fragPosLightSpace) {
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    projCoords = projCoords * 0.5 + 0.5; 
    float closestDepth = texture2D(uShadowMap, projCoords.xy).r;
    float currentDepth = projCoords.z;
  
    float bias = 0.005;
    float shadow = currentDepth - bias > closestDepth ? 0.5 : 0.0;
    return shadow;
  }

  void main(void) {
    vec3 v = normalize(v_normal);
    vec3 l = normalize(light_v);
    vec3 e = normalize(eye_vector);

    vec4 ambient = ambient_coef * light_color; 

    float ndotl = max(dot(v, l), 0.0);  

    vec4 diffuse = diffuse_coef * light_color* ndotl;

    // calculated the reflected light vector R (you can also use the formula R = 2(L.N)N - L)
    vec3 R= normalize(vec3(reflect(-l, v))); 
    float rdotv = max(dot(R, e), 0.0);

    // make sure N.L is larger than 0, this can be redundent
    vec4 specular;  
    if (ndotl>0.0) 
      specular = specular_coef* light_color*pow(rdotv, mat_shininess); 
    else
      specular = vec4(0,0,0,1);  

    vec4 phong = ambient+diffuse+specular; 

    float shadow = shadowCalculation(vFragPosLightSpace);

    gl_FragColor = (1.0-shadow) * phong + shadow * vec4(0.0, 0.0, 0.0, 1.0);
  }
  
</script>

<!--  ************** Vertex Shader ************* --> 
<script id="shader-vs" type="x-shader/x-vertex">

  attribute vec3 aVertexPosition;
  attribute vec3 aVertexNormal;

  uniform mat4 uMMatrix;
  uniform mat4 uVMatrix;
  uniform mat4 uPMatrix; 
  uniform mat4 uNMatrix;  // convert normals from world to eye
  uniform vec4 light_vector; 
  uniform mat4 uLightSpaceMatrix;

  varying vec4 eye_pos;  //vertex position in eye space 
  varying vec3 v_normal;  // vertex normal
  varying vec3 light_v;
  varying vec3 eye_vector;
  varying vec4 vFragPosLightSpace;

  void main(void) {

    // transform normal from local to eye space: normal matrix is the inverse transpose of the modelview matrix 
    v_normal =vec3(uNMatrix*vec4(aVertexNormal,0.0));

    // transform the vertex position to eye space 
    vec4 worldPos = uMMatrix * vec4(aVertexPosition, 1.0);
    eye_pos = uVMatrix * worldPos;
    vFragPosLightSpace = uLightSpaceMatrix * worldPos;

    // eye vector V = e-p, where e is (0,0,0) 
    eye_vector = -vec3(eye_pos);

    light_v = vec3(uNMatrix*light_vector);
    // uncomment the line below to define the point light source at eye location
    // light_v = vec3(-eye_pos);

    gl_Position = uPMatrix* eye_pos;

  }
</script>

<!--  ************** Fragment Shader ************* --> 
<script id="depth-fs" type="x-shader/x-fragment">
  precision mediump float;
  void main() { }
</script>

<!--  ************** Vertex Shader ************* --> 
<script id="depth-vs" type="x-shader/x-vertex">
  attribute vec3 aPosition;
  uniform mat4 uModel;
  uniform mat4 uLightSpaceMatrix;
  
  void main() {
    gl_Position = uLightSpaceMatrix * uModel * vec4(aPosition, 1.0);
  }
</script>

</head>


<body onload="webGLStart();">

    <canvas id="myCanvas" width="600"height="600"></canvas>



<br>
  <br>
    <footer>
      <center>
        <p>Example Created by &copy; Haoyu Li</p>
      </center>
    </footer>
  </br>
  <!-- Le javascript
    ================================================== -->
  <!-- Placed at the end of the document so the pages load faster -->
  <script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.8.1/gl-matrix-min.js"></script>
  <!-- <script type="text/javascript" src="glMatrix-0.9.5.min.js"></script> -->
  <script type="text/javascript" src="shaders_setup.js"></script>
  <script type="text/javascript" src="torii_with_shadow.js"></script>
<br/>

</body>


</html>

